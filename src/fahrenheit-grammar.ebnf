program = program-statement+

<program-statement> =
    about
|   citation

about =
    <'about'> <'{'>
        about-statement+
    <'}'>

<about-statement> =
    id
|   title
|   title-short
|   url
|   template
|   documentation
|   author
|   contributor
|   issn
|   eissn
|   issnl
|   citation-format
|   field
|   summary
|   published
|   updated
|   license

<person-statement> =
    email
|   website

id = <'id'> str
url = <'url'> str langcode?
template = <'template'> str langcode?
documentation = <'documentation'> str langcode?
email = <'email'> str
website = <'website'> str
author = (<'author'> str) | (<'author'> str <'{'> person-statement+ <'}'>)
contributor = (<'contributor'> str) | (<'contributor'> str <'{'> person-statement+ <'}'>)
issn = <'issn'> str
eissn = <'eissn'> str
issnl = <'issnl'> str
citation-format = <'citation-format'> str
field = <'field'> str
summary = <'summary'> str langcode?
published = <'published'> str
updated = <'updated'> str
title = <'title'> str langcode?
title-short = <'title-short'> str langcode?
license = <'license'> str

prefix = <'--prefix'> str
suffix = <'--suffix'> str
delimiter = <'--delimiter'> str
font-style = <'--'> ('italique' | 'oblique')
font-variant = <'--'> 'small-caps'
font-weight= <'--'> ('bold' | 'light-bold')
text-decoration = <'--'> 'underline'
vertical-align = <'--'> ('sup' | 'sub')
display = <'--'> ('left' | 'right' | 'indent')

<modifier> = prefix
           | suffix
           | delimiter
           | font-style
           | font-variant
           | font-weight
           | text-decoration
           | vertical-align
           | display

modifiers = modifier+

print = <'print'> modifiers? (var-txt | var-num)

var-txt = <'$'>'title'

var-num = <'$'>'chapter-number'
        | <'$'>'collection-number'
        | <'$'>'edition'
        | <'$'>'issue'
        | <'$'>'number'
        | <'$'>'number-of-pages'
        | <'$'>'number-of-volumes'
        | <'$'>'volume'

<langcode> = <'('> #'[a-zA-Z-]+' <')'>
str = <'"'> chr* <'"'>
<chr> = #'[^"]'

citation =
    <'citation'> <'{'>
        modifiers?
        print*
    <'}'>