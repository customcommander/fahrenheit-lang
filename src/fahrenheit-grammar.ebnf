program = program-statement+

<program-statement> =
    about
|   citation

about =
    <'about'> <'{'>
        meta-statement+
    <'}'>

<person-detail-statement> = email
                          | url

meta-statement = id
               | issn
               | eissn
               | issnl
               | title
               | title-short
               | summary
               | published
               | updated
               | citation-format
               | field-statement
               | license
               | url
               | documentation
               | template
               | person-statement

id = <'id'> str
url = <'url'> str lang?
template = <'template'> str lang?
documentation = <'documentation'> str lang?
email = <'email'> str
author = (<'author'> str) | (<'author'> str <'{'> person-detail-statement+ <'}'>)
contributor = (<'contributor'> str) | (<'contributor'> str <'{'> person-detail-statement+ <'}'>)
person-statement = author | contributor
issn = <'issn'> str
eissn = <'eissn'> str
issnl = <'issnl'> str
citation-format = <'citation-format'> str
field-statement = <'field'> str
summary = <'summary'> str lang?
published = <'published'> str
updated = <'updated'> str
title = <'title'> str lang?
title-short = <'title-short'> str lang?
license = <'license'> str lang?

prefix = <'--prefix'> str
suffix = <'--suffix'> str
delimiter = <'--delimiter'> str
font-style = <'--'> ('italique' | 'oblique')
font-variant = <'--'> 'small-caps'
font-weight= <'--'> ('bold' | 'light-bold')
text-decoration = <'--'> 'underline'
vertical-align = <'--'> ('sup' | 'sub')
display = <'--'> ('left' | 'right' | 'indent')

<modifier> = prefix
           | suffix
           | delimiter
           | font-style
           | font-variant
           | font-weight
           | text-decoration
           | vertical-align
           | display

modifiers = modifier+

print = <'print'> modifiers? (var-txt | var-num)

var-txt = <'$'>'title'

var-num = <'$'>'chapter-number'
        | <'$'>'collection-number'
        | <'$'>'edition'
        | <'$'>'issue'
        | <'$'>'number'
        | <'$'>'number-of-pages'
        | <'$'>'number-of-volumes'
        | <'$'>'volume'

<lang> = <'('> #'[a-zA-Z-]+' <')'>
str = <'"'> chr* <'"'>
<chr> = #'[^"]'

citation =
    <'citation'> <'{'>
        modifiers?
        print*
    <'}'>